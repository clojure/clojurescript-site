= Code Splitting
David Nolen
2017-07-10
:type: guides
:toc: macro
:icons: font

ifdef::env-github,env-browser[:outfilesuffix: .adoc]

> **NOTE**: This guide requires ClojureScript 1.9.854 or later.

As client applications become larger it becomes desirable to load only the code
actually required to run a particular logical screen. Previously ClojureScript
`:modules` compiler option permitted such code splitting, but this feature only
worked under `:advanced` compilation and users would still have to manage
loading these splits. `:modules` also required manual explicit placement of many
entries to produce optimal splits otherwise dependencies would get moved to
`:cljs-base`.

All of these issues are now addressed directly in ClojureScript. This guide will
walk you through code splitting a simple project and demonstrate these new
enhancements.

=== Make a Simple Project

Create a project folder:

[source,bash]
```
mkdir -p hello-modules
cd hello-modules
mkdir src
```

Copy the `cljs.jar` into this directory.

Create a REPL script:

[source,bash]
```
touch repl.clj
```

Edit this script to look like the following:

[source,clojure]
```
(require '[cljs.repl :as r])
(require '[cljs.build.api :as b])
(require '[cljs.repl.browser :as rb])

(def opts
  {:watch "src"
   :output-dir "out"
   :asset-path "/out"
   :optimizations :none
   :modules {:foo {:entries '#{foo.core}
                   :output-to "out/foo.js"}
             :bar {:entries '#{bar.core}
                   :output-to "out/bar.js"}}
   :browser-repl true
   :verbose true})

(b/build "src" opts)
(r/repl* (rb/repl-env) opts)
```

Now make an `index.html` file:

[source,html]
```
<html>
    <body>
         <button id="button">Load Bar!</button>
         <script src="out/cljs_base.js" type="text/javascript"></script>
         <script src="out/foo.js" type="text/javascript"></script>
    </body>
</html>
```

=== The Sources

Create the `foo.core` namespace:

[source,bash]
```
mkdir -p src/foo
touch src/foo/core.cljs
```

Edit this file to look like the following:

[source,clojure]
```
(ns foo.core
  (:require [goog.dom :as gdom]
            [goog.events :as events]
            [cljs.loader :as loader])
  (:import [goog.events EventType]))

(enable-console-print!)

(println "I'm foo!")

(events/listen (gdom/getElement "button") EventType.CLICK
  (fn [e]
    (loader/load :bar
      (fn []
        ((resolve 'bar.core/woz))))))
```

Notice the unfamiliar namespace `cljs.loader`. This namespace provides a Google
Closure ModuleManager singleton to manage the loading of code splits. This
manager will be initialized with whatever module graph you have defined in
`:modules`.

When the user clicks the button we load the `:bar` module and invoke a function
that exists in the `bar.core` namespace. Notice that we use `resolve`. This is
because we cannot directly call something we never required. If we tried to do
this without `resolve` the ClojureScript compiler would emit an undeclared var
warning during compilation.

Create the `bar.core` namespace:

[source,bash]
```
mkdir -p src/bar
touch src/bar/core.clj
```
[source,clojure]

```
(ns bar.core
  (:require [cljs.loader :as loader]))

(enable-console-print!)

(println "I'm bar!")

(defn woz []
  (println "WOZ!"))
```

=== Build the Project

Run the REPL script:

[source,bash]
```
rlwrap java -cp cljs.jar:src clojure.main repl.clj
```

You will be prompted to connect via browser. Navigate to
http://localhost:9000/index.html.

Click the button. You will see that the `:bar` module gets loaded and the
function in the other namespace gets invoked.

=== Release Builds

Make a new script `release.clj`:

[source,bash]
```
touch release.clj
```

Make it look like the following:

[source,clojure]
```
(require '[cljs.build.api :as b])

(b/build "src"
  {:output-dir "out"
   :asset-path "/out"
   :optimizations :advanced
   :verbose true
   :modules {:foo {:entries '#{foo.core}
                   :output-to "out/foo.js"}
             :bar {:entries '#{bar.core}
                   :output-to "out/bar.js"}}})

(System/exit 0)
```

Build your project:

[source,bash]
```
java -cp cljs.jar:src clojure.main release.clj
```

Start a browser REPL just for serving content from current directory:

[source,bash]
```
java -jar cljs.jar -m cljs.repl.browser
```

Navigate to http://localhost:9000/index.html. Your application should function
correctly even though advanced compiled.

Change the `foo.core` to take a new require like `cljs.reader`. Rebuild.

You should see that `cljs.reader` gets moved into the `:foo` module but not
`:bar`.

If you examine the split files in `out` you will see that `foo.js` is larger
than `bar.js`.

=== Common Problems

The compiler can normally walk your dependency graph to find the namespaces that are needed by your module; however, there are some libraries that do their own analysis (for example, devcards) of the source and cause the compiler to be unable to see the complete graph. In those cases you will need to list all of the namespaces that are dynamically resolved in the correct module. One symptom of this problem is the following message when you try to load a module: "Assert failed: Module  does not exist" (with a blank name...it should say "Module :name does not exist"). The module system needs to be able to figure out what module the load is being executed from, and this message means it failed to do so, which means that the file that is calling `load` did not get recognized as part of a configured module. To resolve this: add the namespace issuing the `load` to the explicit `:entries` in your compiler config to make sure that namespace gets associated with the module you expect it to be a part of.

Another problem that comes up is a failure for your callback to execute. If you enable full logging in your Javascript console you'll see the callback get registered, the module load, but the code won't actually execute. This happens if you fail to `require` the `cljs.loader` namespace in the *target* module. You must require it even if that module does not need to explicitly use it.
